PREFIX sp: <http://example.org/smartphone#>
PREFIX spv: <http://example.org/smartphone/vocab/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

# CQ3: Inference-based Recommendation Using Constructed Classes
# Question: Find phones that are both "LargeBatteryPhone" and "HighResolutionCameraPhone"
#           with good overall sentiment (uses OWL inference)

SELECT DISTINCT ?phoneName ?brandName
       ?batteryCapacity ?mainCameraMP
       ?avgPrice
       (COALESCE(?posOverall, 0) AS ?positiveOverall)
       (COALESCE(?negOverall, 0) AS ?negativeOverall)
       ((COALESCE(?posOverall, 0) - COALESCE(?negOverall, 0)) AS ?sentimentScore)
WHERE {
  # Use inferred classes (constructed via SPARQL CONSTRUCT)
  ?phone a sp:LargeBatteryPhone ;
         a sp:HighResolutionCameraPhone ;
         a sp:InMarketPhone ;
         sp:phoneName ?phoneName ;
         sp:hasBrand ?brand ;
         sp:batteryCapacityMah ?batteryCapacity ;
         sp:mainCameraMP ?mainCameraMP .

  ?brand sp:brandName ?brandName .

  # Get average price
  {
    SELECT ?phone (AVG(?priceValue) AS ?avgPrice)
    WHERE {
      ?variant sp:variantOf ?phone ;
               sp:hasPrice ?price .
      ?price sp:priceValue ?priceValue ;
             sp:currency "EUR" .
    }
    GROUP BY ?phone
  }

  # Overall sentiment
  OPTIONAL {
    SELECT ?phone (COUNT(?review) AS ?posOverall)
    WHERE {
      ?review sp:reviewOf ?phone ;
              sp:hasPositiveSentimentTag spv:Overall .
    }
    GROUP BY ?phone
  }

  OPTIONAL {
    SELECT ?phone (COUNT(?review) AS ?negOverall)
    WHERE {
      ?review sp:reviewOf ?phone ;
              sp:hasNegativeSentimentTag spv:Overall .
    }
    GROUP BY ?phone
  }
}
ORDER BY DESC(?sentimentScore) ?avgPrice
LIMIT 10
