PREFIX sp: <http://example.org/smartphone#>
PREFIX spv: <http://example.org/smartphone/vocab/>

# CQ8: Collaborative filtering - Find similar users and their liked phones
# Question: What phones do similar users (with shared interests) like?

SELECT ?phoneName ?brandName ?similarityScore ?avgPrice
WHERE {
  {
    SELECT ?recommendedPhone ?phoneName ?brandName
           (COUNT(DISTINCT ?similarUser) AS ?similarityScore)
           (AVG(?priceValue) AS ?avgPrice)
    WHERE {
      # Target user's interests
      ?targetUser sp:userId "gamer_0000" ;
                  sp:interestedIn ?sharedInterest .

      # Find users with similar interests
      ?similarUser sp:interestedIn ?sharedInterest ;
                   sp:likes ?recommendedPhone .

      ?recommendedPhone sp:phoneName ?phoneName ;
                        sp:hasBrand ?brand .

      ?brand sp:brandName ?brandName .

      # Price
      OPTIONAL {
        ?recommendedPhone sp:priceEUR ?priceValue .
      }

      # Exclude phones target user already likes
      FILTER NOT EXISTS {
        ?targetUser sp:likes ?recommendedPhone .
      }

      # Exclude target user from similarity calculation
      FILTER(?similarUser != ?targetUser)
    }
    GROUP BY ?recommendedPhone ?phoneName ?brandName
    HAVING (?similarityScore >= 2)
  }
}
ORDER BY DESC(?similarityScore) ?avgPrice
LIMIT 10
